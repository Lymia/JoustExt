// Copyright (C) 2014 Lymia Aluysia <lymiahugs@gmail.com>
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is furnished
// to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

raw +margins "|Nyuroki Magical Fantasy by Lymia Aluysia
              |Released under the terms of MIT license
              |
              |"

 >>>>>>>>++<--<+<--
 // 61 of each. Written out here to workaround a rather serious Arena.py bug.
<+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
<-------------------------------------------------------------
<-------------------------------------------------------------
<+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
<(-)*(128-109) // Change our home flag to throw off simpler bots.

// And, then let's just rush!
@wiggleClear($neg0, $pos0, $neg1, $pos1, $neg2, $pos2) {
  @wiggleClearPos($adjustment, $count) {
    if($count == 0) {
      (-)*$adjustment
      @clearBody()
    } else {
      +[
        @wiggleClearPos($adjustment, $count - 1)
      ]
    }
  }
  @wiggleClearNeg($adjustment, $count) {
    if($count == 0) {
      (+)*$adjustment
      @clearBody()
    } else {
      -[
        @wiggleClearNeg($adjustment, $count - 1)
      ]
    }
  }

  @clearBody() {
    @clearBody() {
      @clearBody() {
        @clearBody() {
          @clearBody() {
            @clearBody() {
              (+)*82 [[+.].]
              @break() // In case anyone tries to beat us by locking us in our loop.
            }
            @wiggleClearPos(0, $pos2 - $pos1)
          }
          @wiggleClearNeg($neg2 + $pos1, $neg2 - $neg1)
        }
        @wiggleClearPos($pos1 + $neg1, $pos1 - $pos0)
      }
      @wiggleClearNeg($neg1 + $pos0, $neg1 - $neg0)
    }
    @wiggleClearPos($pos0 + $neg0, $pos0)
  }
  @wiggleClearNeg($neg0, $neg0)
}
(>)*8 // Rule of 9
(
  > callcc(@break) {
    // Clear counts shamelessly adjusted to hill.
    [@wiggleClear(3, 3, 13, 11, 30, 32)]
  }
  --
)*21

// ... oh screw it. Not worth the binary size.
// Used to be (-.--.---.)*-1, but, that only earned me /1/ win. Not a big deal.
terminate
